project(chordalSubgraph)
cmake_minimum_required(VERSION 3.1)
set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/cmake")
set(Boost_USE_STATIC_LIBS	OFF)
set(Boost_USE_MULTITHREADED	ON)
set(Boost_USE_STATIC_RUNTIME	OFF)

option(BUILD_R_PACKAGE "Build R package" ON)
option(USE_NAUTY "Use the Nauty package" ON)
option(BUILD_R_DATA "Build data for R package" OFF)
find_package(sampling REQUIRED CONFIG)

add_definitions("-DBOOST_NO_AUTO_PTR")
if(CMAKE_COMPILER_IS_GNUCC)
	add_definitions("-Wall -fPIC")
endif()

#Find nauty
if(USE_NAUTY)
	find_package(nauty REQUIRED)
endif()

#Find boost packages
find_package(Boost 1.53.0 COMPONENTS graph program_options system serialization filesystem regex REQUIRED)

if(WIN32)
	find_package(mpfr REQUIRED)
	find_package(mpir REQUIRED)
else()
	#mpfr needs to be listed as an imported target so that we can use target_link_librarie later on. If we don't have this then it just adds a -lmpfr which probably picks up the wrong thing. 
	find_library(mpfr_LIBRARY mpfr)
	find_path(mpfr_INCLUDE mpfr.h)
	add_library(mpfr SHARED IMPORTED)
	set_target_properties(mpfr PROPERTIES IMPORTED_LOCATION ${mpfr_LIBRARY})
	set_target_properties(mpfr PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${mpfr_INCLUDE})
endif()

add_subdirectory(chordalGraphCommon)
add_subdirectory(berry2006Test)
add_subdirectory(copyText)
add_subdirectory(copyBinary)
add_subdirectory(stochasticEnumeration)
add_subdirectory(testComputeAutomorphismGroupSize)
add_subdirectory(testFormTree)
if(BUILD_R_PACKAGE)
	add_subdirectory(RPackage)
	find_package(R REQUIRED)
	if(WIN32)
		install(CODE "execute_process(COMMAND \"${R_COMMAND}\" CMD INSTALL --no-multiarch RPackage/\$\{CMAKE_INSTALL_CONFIG_NAME\} WORKING_DIRECTORY ${CMAKE_BINARY_DIR})")
	else()
		install(CODE "execute_process(COMMAND \"${R_COMMAND}\" CMD INSTALL RPackage/ WORKING_DIRECTORY ${CMAKE_BINARY_DIR})")
	endif()
endif()
